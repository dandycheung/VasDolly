apply plugin: 'com.android.application'
apply plugin: 'com.tencent.vasdolly'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }

    dependencies {
        classpath "com.tencent.vasdolly:plugin:3.0.6"
    }
}

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.tencent.vasdolly.app"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("release.keystore")
            storePassword "123456"
            keyAlias "leon"
            keyPassword "123456"
            enableV1Signing true
            enableV2Signing true
            enableV3Signing false
        }

        debug {
            storeFile file("release.keystore")
            storePassword "123456"
            keyAlias "leon"
            keyPassword "123456"
            enableV1Signing true
            enableV2Signing true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    lintOptions {
        abortOnError false
    }
}

// VasDolly 插件配置
// gradle channelDebug/channelRelease 编译生成 apk 后，再根据生成的 apk 生成渠道包
channel {
    channelFile = file("app_channel.txt")
    // 多渠道包的输出目录，默认为 new File(project.buildDir, "channel")
    outputDir = new File(project.buildDir, "channels")
    // 多渠道包的命名规则，默认为：${appName}-${versionName}-${versionCode}-${flavorName}-${buildType}-${buildTime}
    apkNameFormat ='${appName}-${versionName}-${versionCode}-${flavorName}-${buildType}'
    // 快速模式：生成渠道包时不进行校验（速度可以提升 10 倍以上，默认为 false）
    fastMode = false
    // buildTime 的时间格式，默认格式：yyyyMMdd-HHmmss
    buildTimeDateFormat = 'yyyyMMdd-HH:mm:ss'
    // 低内存模式（仅针对 V2 签名，默认为 false）：只把签名块、中央目录和 EOCD 读取到内存，不把最大头的内容块读取到内存，在手机上合成 apk 时，可以使用该模式
    lowMemory = false
}

// gradle rebuildChannel 配置此任务用于不希望重新构建 apk，直接根据指定的 apk 生成渠道包
rebuildChannel {
    channelFile = file("app_channel.txt")
    // 指定的 apk 生成渠道包，文件名中如果有 base 将被替换为渠道名，否则渠道名将作为前缀
    baseApk = new File(project.buildDir, "outputs/apk/debug/app-debug.apk")
    // 默认为 new File(project.buildDir, "rebuildChannel")
    outputDir = new File(project.buildDir, "rebuildChannels")
    // 快速模式：生成渠道包时不进行校验（速度可以提升 10 倍以上，默认为 false）
    fastMode = false
    // 低内存模式（仅针对 V2 签名，默认为 false）：只把签名块、中央目录和 EOCD 读取到内存，不把最大头的内容块读取到内存，在手机上合成 apk 时，可以使用该模式
    lowMemory = false
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])

    api 'androidx.appcompat:appcompat:1.4.0'
    // api：同老版本 compile 作用一样，本 module 将会向外暴露其依赖的 module 变化。此处即
    // 若 helper 库发生了改变，那么本 module 以及所有依赖于本 module 的 module 都要重新编译
    api "com.tencent.vasdolly:helper:3.0.6"
}
